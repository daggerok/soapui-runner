/**
 * The MIT License
 *
 * Copyright 2017 Maksim Kostromin
 *
 * Permission is hereby granted, free of charge, to any person
 * obtaining a copy of this software and associated documentation files
 * (the "Software"), to deal in the Software without restriction,
 * including without limitation the rights to use, copy, modify, merge,
 * publish, distribute, sublicense, and/or sell copies of the Software,
 * and to permit persons to whom the Software is furnished to do so,
 * subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be
 * included in all copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
 * EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
 * MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
 * IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
 * CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
 * TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
 * SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * https://tldrlegal.com/license/mit-license
 */

/*
 * read more: https://guides.gradle.org/publishing-plugins-to-gradle-plugin-portal/
 */

buildscript {
  ext {
    url = 'https://github.com/daggerok/soapui-runner'
  }
}

group = 'io.github.daggerok'
version = "$soapUIVersion-4"
description = info('Allows run SoapUI testrunner and loadtestrunner.')

gradlePlugin {
  plugins {
    soapUITestRunnerPlugin {
      id = "${project.group}.soapui-testrunner"
      implementationClass = "${project.group}.SoapUITestRunnerPlugin"
    }
    soapUILoadTestRunnerPlugin {
      id = "${project.group}.soapui-loadtestrunner"
      implementationClass = "${project.group}.SoapUILoadTestRunnerPlugin"
    }
    soapUIRunnerPlugin {
      id = "${project.group}.soapui-runner"
      implementationClass = "${project.group}.SoapUIRunnerPlugin"
    }
  }
}

publishing {
  repositories {
    maven {
      url '../soapui-runner-example/maven-repo'
    }
  }
}

pluginBundle {

  website = project.url
  vcsUrl = "${project.url}.git"
  description = project.description
  tags = [
      'SoapUI',
      'soapui',
      'smartbear',
      'soapuirunner',
      'smartbear.com',
      'www.soapui.org',
      'testing',
      'SOAP',
      'REST',
      'runner',
      'testrunner',
      'testsuites',
      'testsuite',
      'testcases',
      'testcase',
      'test',
      'loadtestrunner',
      'loadrunner',
      'loadtests',
      'loadtest',
      'load',
      'QA',
  ]

  plugins {
    soapUITestRunnerPlugin {
      id = "${project.group}.soapui-testrunner"
      // max length 254 characters:
      description = displayName = info('Allows run SoapUI TestSuites and TestCases using testrunner.')
    }
    soapUILoadTestRunnerPlugin {
      id = "${project.group}.soapui-loadtestrunner"
      description = displayName = info('Allows run SoapUI Load Tests using loadtestrunner.')
    }
    soapUIRunnerPlugin {
      id = "${project.group}.$rootProject.name"
      displayName = project.description
    }
  }
}

/**
 * Plugin displayName / description helper builder.
 * <br/>
 * @param suffix plugin specific information.
 * @return plugin displayName / description.
 */
static String info(final String suffix) {
  """\
     |SoapUI Runner Gradle Plugin.
     |
     |Please, make sure you have also added required plugin repository in your buildscript: 
     |maven { url 'http://smartbearsoftware.com/repository/maven2/' }
     |
     |$suffix""".stripMargin()
}
